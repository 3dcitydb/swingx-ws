plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '4.16.1'
}

group 'org.citydb'
version '1.1.5'
description 'Fork of the inactive swingx-ws library'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    compileJava {
        options.release = 8
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    maven {
        url 'https://citydb.jfrog.io/artifactory/maven'
    }
    mavenCentral()
}

dependencies {
    implementation 'org.citydb:swingx-all:1.6.9-SNAPSHOT'
    implementation 'commons-httpclient:commons-httpclient:3.0.1'
    implementation 'commons-logging:commons-logging:1.1'
    implementation 'commons-codec:commons-codec:1.3'
    implementation 'xerces:xercesImpl:2.8.1'
    implementation 'com.sun.xml.ws:jaxws-ri:2.3.2'
    implementation 'net.sf.json-lib:json-lib:0.9'
    implementation 'jtidy:jtidy:4aug2000r7-dev'
    implementation 'rome:rome:0.8'
    implementation 'org.json:json:20171018'
    compileOnly 'org.kohsuke.metainf-services:metainf-services:1.8'
    annotationProcessor 'org.kohsuke.metainf-services:metainf-services:1.8'
}

javadoc {
    failOnError = false
    options.addStringOption('Xdoclint:none', '-quiet')
}

jar {
    from(sourceSets.main.allSource) {
        include '**/*'
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/3dcitydb'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License (LGPL), Version 2.1'
                        url = 'http://www.fsf.org/licensing/licenses/lgpl.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'clausnagel'
                        name = 'Claus Nagel'
                        email = 'claus.nagel@gmail.com'
                        roles = ['architect', 'developer']
                        timezone = '+1'
                    }
                }
                scm {
                    url = 'https://github.com/3dcitydb/swingx-ws'
                    connection = 'scm:git:https://github.com/3dcitydb/swingx-ws.git'
                }
            }
        }
    }
}

artifactory {
    contextUrl = project.hasProperty('artifactory_contextUrl') ? project.property('artifactory_contextUrl') : System.getenv('artifactory_contextUrl')
    publish {
        repository {
            repoKey = 'maven'
            username = project.hasProperty('artifactory_user') ? project.property('artifactory_user') : System.getenv('artifactory_user')
            password = project.hasProperty('artifactory_password') ? project.property('artifactory_password') : System.getenv('artifactory_password')
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
}