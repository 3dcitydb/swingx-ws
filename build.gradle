plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group 'org.citydb'
version '1.1.1'
description 'Fork of the inactive swingx-ws library'

sourceCompatibility = 1.8

repositories {
    maven {
        url 'https://dl.bintray.com/3dcitydb/maven'
    }
    jcenter()
}

dependencies {
    compile 'org.citydb:swingx-all:1.6.6'
    compile 'jdom:jdom:1.0'
    compile 'commons-httpclient:commons-httpclient:3.0.1'
    compile 'commons-logging:commons-logging:1.1'
    compile 'commons-codec:commons-codec:1.3'
    compile 'xerces:xercesImpl:2.8.1'
    compile ('xml-apis:xml-apis:1.0.b2') {
        force = true
    }
    compile('net.sf.json-lib:json-lib:0.9')
    compile 'jtidy:jtidy:4aug2000r7-dev'
    compile 'rome:rome:0.8'
    compile 'org.json:json:20171018'
    compileOnly 'org.kohsuke.metainf-services:metainf-services:1.3'
    annotationProcessor 'org.kohsuke.metainf-services:metainf-services:1.3'
}

javadoc {
    failOnError = false
    options.addStringOption('Xdoclint:none', '-quiet')
}

jar {
    from(sourceSets.main.allSource) {
        include '**/*'
    }
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/3dcitydb'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License (LGPL), Version 2.1'
                        url = 'http://www.fsf.org/licensing/licenses/lgpl.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'clausnagel'
                        name = 'Claus Nagel'
                        email = 'claus.nagel@gmail.com'
                        roles = ['architect', 'developer']
                        timezone = '+1'
                    }
                }
                scm {
                    url = 'https://github.com/3dcitydb/swingx-ws'
                    connection = 'scm:git:https://github.com/3dcitydb/swingx-ws.git'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        userOrg = '3dcitydb'
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/3dcitydb'
        vcsUrl = 'https://github.com/3dcitydb/swingx-ws.git'
        issueTrackerUrl = 'https://github.com/3dcitydb/swingx-ws/issues'
        licenses = ['LGPL-2.1']
        version {
            name = project.version
        }
    }
}